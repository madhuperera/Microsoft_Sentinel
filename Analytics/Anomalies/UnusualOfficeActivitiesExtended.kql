let BaselinePreiod = 14d;
let ReportPeriod = 1d;
let BinPeriod = ReportPeriod;
let UnmonitoredUserIds = dynamic(["SHAREPOINT\\system"]);
let MonitoredOperations = dynamic([
    "AddedToSecureLink",
    "AddedToSharingLink",
    "ChannelDeleted",
    "CompanyLinkCreated",
    "ConnectorAdded",
    "FileDeleted",
    "FileDeletedFirstStageRecycleBin",
    "FileDownloaded",
    "FileSyncDownloadedFull",
    "FolderDeleted",
    "FolderDeletedFirstStageRecycleBin",
    "HardDelete",
    "ListDeleted",
    "ListItemDeleted",
    "MemberRemoved",
    "New-InboxRule",
    "SecureLinkCreated",
    "SendAs",
    "SharingLinkCreated",
    "SharingLinkUpdated",
    "SiteDeleted",
    "TeamDeleted",
    "UpdateInboxRules"
    ]);
let OperationThresholds = datatable(Operation: string, BaselineCountThreshold: int, CurrentCountThreshold: int, ChangeThreshold: int)
[
    "AddedToSecureLink", 0, 5, 100,
    "AddedToSharingLink", 0, 5, 100,
    "ChannelDeleted", 0, 1, 100,
    "CompanyLinkCreated", 0, 5, 100,
    "ConnectorAdded", 0, 1, 100,
    "FileDeleted", 10, 100, 100,
    "FileDeletedFirstStageRecycleBin", 7, 100, 100,
    "FileDownloaded", 10, 100, 100,
    "FileSyncDownloadedFull", 10, 100, 100,
    "FolderDeleted", 7, 100, 100,
    "FolderDeletedFirstStageRecycleBin", 7, 100, 100,
    "HardDelete", 10, 100, 100,
    "ListDeleted", 0, 0, 100,
    "ListItemDeleted", 5, 10, 100,
    "MemberRemoved", 0, 0, 100,
    "New-InboxRule", 0, 0, 100,
    "SecureLinkCreated", 0, 5, 100,
    "SendAs", 5, 20, 100,
    "SharingLinkCreated", 0, 5, 100,
    "SharingLinkUpdated", 0, 1, 100,
    "SiteDeleted", 0, 0, 100,
    "TeamDeleted", 0, 0, 100,
    "UpdateInboxRules", 0, 1, 100
];
let HistoricData = OfficeActivity
| where TimeGenerated between (ago(BaselinePreiod) .. ago(ReportPeriod))
| where Operation in (MonitoredOperations) and UserId !in (UnmonitoredUserIds)
| where UserType == "Regular"
| summarize H_Total = count() by bin(TimeGenerated, BinPeriod), UserId, Operation
| summarize H_DailyCount = toint(avg(H_Total)) by UserPrincipalName = UserId, Operation
| sort by UserPrincipalName asc, Operation asc, H_DailyCount desc;
let RecentData = OfficeActivity
| where TimeGenerated > ago(ReportPeriod)
| where Operation in (MonitoredOperations) and UserId !in (UnmonitoredUserIds)
| where UserType == "Regular"
| summarize R_Total = count() by UserPrincipalName = UserId, Operation
| sort by UserPrincipalName asc, Operation asc, R_Total desc;
let CompletedResults = HistoricData
| join kind=inner RecentData on UserPrincipalName, Operation
| extend BaselineCount = H_DailyCount
| extend CurrentCount = R_Total
| extend ChangeRatio = round(((todouble(CurrentCount) - todouble(BaselineCount)) / todouble(BaselineCount)) * 100, 2)
| project UserPrincipalName, Operation, BaselineCount, CurrentCount, ChangeRatio 
| order by UserPrincipalName asc, Operation asc;
let UserList = SigninLogs
| where TimeGenerated > ago(BaselinePreiod)
| where UserType == "Member" and ResultType == 0
| distinct UserObjectId = UserId, UserDisplayName, UserPrincipalName
| sort by UserPrincipalName asc;
CompletedResults
| join kind=leftouter UserList on UserPrincipalName
| join kind=fullouter OperationThresholds on Operation
//| where ChangeRatio > ChangeThreshold and BaselineCount > BaselineCountThreshold and CurrentCount > CurrentCountThreshold
| where (BaselineCount > BaselineCountThreshold and CurrentCount > CurrentCountThreshold and CurrentCount > BaselineCount) or (ChangeRatio > ChangeThreshold)
| project UserDisplayName, UserPrincipalName, UserObjectId, Operation, BaselineCountThreshold, BaselineCount, CurrentCountThreshold, CurrentCount, ChangeThreshold, ChangeRatio
| order by UserDisplayName asc, Operation asc