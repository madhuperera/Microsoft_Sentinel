// Get the monitored group from Watchlist
let MonitoredADGroupNames = _GetWatchlist('Privileged_Groups')
    | project Name;
// First Define a List of Categories to Monitor
let MonitoredCategories = dynamic(["GroupManagement"]);
// Define a List of Operations to Monitor
let MonitoredOperations = dynamic(["Add member to group", "Add owner to group", "Update group", "Remove member from group", "Hard Delete group", "Delete group", "Remove owner from group"]);
// Define how far you want to check the logs for
let NumOfDays = 1d;
AuditLogs
| where TimeGenerated > ago(NumOfDays)
| where Category in (MonitoredCategories)
| where OperationName in (MonitoredOperations)
| extend TargetedGroupName = iff(
    OperationName contains "Update group",
    tostring(parse_json(TargetResources[0]).displayName),
    iff(
    OperationName contains "Add",
    tostring(parse_json(TargetResources[0]).modifiedProperties[1].newValue),
    iff(OperationName contains "Remove", tostring(parse_json(TargetResources[0]).modifiedProperties[1].oldValue), "Unexpected Condition for Group Name")
    )
    )
| extend PerformedOn = iff(
    OperationName contains "Add member to group" or OperationName contains "Remove member from group",
    tostring(parse_json(tostring(TargetResources[0].userPrincipalName))), "N/A")
| extend TargetedGroupName = trim("\"", TargetedGroupName)
| extend PerformedBy = tostring(parse_json(tostring(InitiatedBy.user)).userPrincipalName)
| where TargetedGroupName in (MonitoredADGroupNames)
| project TimeGenerated, OperationName, TargetedGroupName, ActivityDisplayName, PerformedBy, PerformedOn
| sort by TimeGenerated desc